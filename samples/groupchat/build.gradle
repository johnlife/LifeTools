apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0-rc1'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':..:libraries:LifeToolsNew')
    compile project(':..:libraries:LifeTools Firebase')
    compile project(':..:libraries:CropImage')
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    def Properties versionProps = new Properties()
    def versionMajor = 1
    def versionMinor =  1
    def versionBuild = 1
    def versionNumber = 1
    def versionPropsFile = file('version.properties')

    gradle.taskGraph.whenReady {taskGraph ->
        if (taskGraph.hasTask(assembleRelease)) {
            versionProps['RELEASED'] = 'true';
            versionProps.store(versionPropsFile.newWriter(), null)
        }
    }

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
        def released = (versionProps['RELEASED'] != null) && (versionProps['RELEASED'].asBoolean())
        def increment = 0
        if (released) {
            increment = 1
            versionProps.remove('RELEASED');
        }
        versionMinor =  versionProps['VERSION_MINOR'].toInteger() + increment
        versionBuild =  versionProps['VERSION_BUILD'].toInteger() + 1
        versionNumber = versionProps['VERSION_NUMBER'].toInteger() + increment
    }

    versionProps['VERSION_MINOR'] = versionMinor.toString()
    versionProps['VERSION_BUILD'] = versionBuild.toString()
    versionProps['VERSION_NUMBER'] = versionNumber.toString()

    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
        versionCode versionNumber
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"
        minSdkVersion 14
        targetSdkVersion 23
        applicationId "com.groupchat"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def fileNaming = "apk/${applicationId}"
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk') && outputFile.name.contains('release')) {
                output.outputFile = new File(getProject().getRootDir(), "${fileNaming}-${versionMajor}.${versionMinor}.${versionBuild}.apk")
            }
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

}
